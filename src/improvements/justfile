# Create a new template or task
# e.g. just new [template|task]
new COMMAND="" TASK_TYPE="":
    #!/usr/bin/env bash
    set -euo pipefail
    
    echo -e "\033[42m                          \033[0m"
    echo -e "\033[42m    superchain-ops cli    \033[0m"
    echo -e "\033[42m                          \033[0m"

    show_usage() {
        echo
        echo "Usage: just new [template|task]"
        echo "Available commands:"
        echo "  • template  - Create a new template"
        echo "  • task      - Create a new task"
        echo
    }

    show_task_types() {
        echo
        echo "Available task types:"
        echo "  • l2taskbase - 'L2TaskBase' task type, these tasks require a superchain address registry and can iterate over multiple L2 chains"
        echo "    eg: https://github.com/ethereum-optimism/superchain-ops/blob/main/test/tasks/mock/template/GasConfigTemplate.sol"
        echo "  • simplebase - 'SimpleBase' task type, these tasks require simple key value registry and are independent of the L2 chains"
        echo "    eg: https://github.com/ethereum-optimism/superchain-ops/blob/main/src/improvements/template/FinanceTemplate.sol"
        echo "  • opcmbasetask - 'OPCMBaseTask' task type, these tasks are used to make delegate calls to the Optimism Contracts Manager and are"
        echo "    dependent on the L2 chains and inherit from L2TaskBase"
        echo "    e.g.: https://github.com/ethereum-optimism/superchain-ops/blob/main/src/improvements/template/OPCMUpgradeV200.sol"
        echo
    }
    
    # shellcheck disable=SC2050
    case "{{COMMAND}}" in
        "")
            echo -e "\n\033[31mError: No command specified\033[0m"
            show_usage
            exit 1
            ;;
        template)
            if [ -z "{{TASK_TYPE}}" ]; then
                echo -e "\n\033[31mError: No task type specified\033[0m"
                show_task_types
                exit 1
            fi

            case "{{TASK_TYPE}}" in
                l2taskbase)
                    TASK_TYPE="L2TaskBase"
                    ;;
                simplebase)
                    TASK_TYPE="SimpleBase"
                    ;;
                opcmbasetask)
                    TASK_TYPE="OPCMBaseTask"
                    ;;
                *)
                    echo -e "\n\033[31mError: Invalid task type '{{TASK_TYPE}}'\033[0m"
                    show_task_types
                    exit 1
                    ;;
            esac
            ./script/create-template.sh "$TASK_TYPE"
            ;;
        task)
            if [ -n "{{TASK_TYPE}}" ]; then
                echo -e "\n\033[31mError: Task type should not be specified for 'task' command\033[0m"
                exit 1
            fi
            ./script/create-task.sh
            ;;
        *)
            echo -e "\n\033[31mError: Invalid command '{{COMMAND}}'\033[0m"
            show_usage
            exit 1
            ;;
    esac

task COMMAND="" NETWORK="":
    #!/usr/bin/env bash
    set -euo pipefail

    echo -e "\033[42m                          \033[0m"
    echo -e "\033[42m    superchain-ops cli    \033[0m"
    echo -e "\033[42m                          \033[0m"

    if [ "{{COMMAND}}" = "" ]; then
        echo -e "\n\033[31mError: No command specified\033[0m\n"
        exit 1
    fi

    # shellcheck disable=SC2194
    case "{{COMMAND}}" in
        ls)
            echo ""
            ./script/sorted-tasks.sh {{NETWORK}}
            ;;
        *)
            echo -e "\n\033[31mError: Invalid command '{{COMMAND}}'\033[0m\n"
            exit 1
            ;;
    esac
    
monorepo-integration-test COMMAND="":
    #!/usr/bin/env bash
    set -euo pipefail

    # Set FOUNDRY_PROFILE, defaulting to 'default' if not already set.
    export FOUNDRY_PROFILE="${FOUNDRY_PROFILE:-default}"
    echo "Currently running with FOUNDRY_PROFILE: ${FOUNDRY_PROFILE}"

    root_dir=$(git rev-parse --show-toplevel)
    allocs_path="${root_dir}/lib/optimism/packages/contracts-bedrock/allocs.json"

    # Running this command with mainnet RPC URL.
    ETH_RPC_URL=$(yq eval ".profile.\"${FOUNDRY_PROFILE}\".rpc_endpoints.mainnet" "${root_dir}/foundry.toml")
    export ETH_RPC_URL
    echo "Using mainnet RPC: ${ETH_RPC_URL}"
    
    # For now, we are running monorepo integration tests for the example eth tasks only.
    mainnet_network_task_dir="eth" 

    forge build
    forge script ${root_dir}/src/improvements/tasks/TaskRunner.sol:TaskRunner --sig "run(string,string)" ${allocs_path} ${mainnet_network_task_dir} --ffi --rpc-url $ETH_RPC_URL
    export SUPERCHAIN_OPS_ALLOCS_PATH=./allocs.json
    cd ${root_dir}/lib/optimism/packages/contracts-bedrock/

    # When deciding which tests to skip, start with an empty list. Run the tests locally, 
    # record any failures, decide which are acceptable to skip, and then add those back to the list.
    # SystemConfig_upgrade_Test - Should not be failing.
    # L1CrossDomainMessenger_Test - Should not be failing.
    # L1ERC721Bridge_Test - Should not be failing.
    NO_MATCH_CONTRACTS="OptimismPortal2_FinalizeWithdrawal_Test\|"
    NO_MATCH_CONTRACTS+="AnchorStateRegistry_*\|"
    NO_MATCH_CONTRACTS+="FaultDisputeGame_Test\|"
    NO_MATCH_CONTRACTS+="PermissionedDisputeGame_Test\|"
    NO_MATCH_CONTRACTS+="FaultDispute_1v1_Actors_Test\|"
    NO_MATCH_CONTRACTS+="DelayedWETH_Hold_Test\|"
    NO_MATCH_CONTRACTS+="ETHLockboxTest\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1StandardBridge_BridgeETHTo_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1StandardBridge_BridgeETH_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1StandardBridge_DepositETHTo_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1StandardBridge_DepositETH_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1StandardBridge_Receive_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="OptimismPortal2_LiquidityMigration_Test\|" # has a dependency on OptimismPortal migrateLiquidity function
    NO_MATCH_CONTRACTS+="OptimismPortal2_Test\|" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="OptimismPortal2_upgrade_Test" # has a dependency on ETHLockbox
    NO_MATCH_CONTRACTS+="L1CrossDomainMessenger_Test" # has a dependency on ETHLockbox
    export NO_MATCH_CONTRACTS
    # shellcheck disable=SC2194
    case "{{COMMAND}}" in
        rerun)
            just test-upgrade-rerun --no-match-contract "${NO_MATCH_CONTRACTS}"
            ;;
        *)
            just test-upgrade --no-match-contract "${NO_MATCH_CONTRACTS}"
            ;;
    esac
    rm -f ${allocs_path} # clean up

check-superchain-registry-latest:
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)
    bash ${root_dir}/src/improvements/script/check-superchain-latest.sh

# this is command is mainly for CI testing purposes and not task developers
# task developers can run this locally, however it is expected that this
# command will be run mostly by CI.
simulate-all-templates:
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)
    forge build

    # If the task is nested then we only simulate as the foundation.
    # In the future we could simulate as other nested safes. 
    # For testing purposes, we do not gain anything by simulating as other nested safes.
    nested_safe_name="foundation"
    simulation_count=0
    for task in ${root_dir}/test/tasks/example/*/*; do
        if [ -d "$task" ]; then
            ${root_dir}/src/improvements/script/simulate-task.sh $task $nested_safe_name
            simulation_count=$((simulation_count + 1))
        fi
    done
    echo "$simulation_count simulations run."
    template_count=$(find "${root_dir}/src/improvements/template" -type f ! -name "*.template.sol" | wc -l)

    if [ "$simulation_count" -ne "$template_count" ]; then
        echo "Not all templates files have a corresponding task. Please add a task for a template in ${root_dir}/src/improvements/tasks/example" >&2
        exit 1
    fi

simulate-verify-task TASK="":
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)

    forge build

    # If the task is nested then we only simulate as the foundation.
    # In the future we could simulate as other nested safes. 
    # For testing purposes, we do not gain anything by simulating as other nested safes.
    nested_safe_name="foundation"

    ${root_dir}/src/improvements/script/simulate-verify-task.sh {{TASK}} $nested_safe_name
