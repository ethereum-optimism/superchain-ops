// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

/// @title Multicall3
/// @notice Aggregate results from multiple function calls
/// @dev Multicall & Multicall2 backwards-compatible
/// @dev Aggregate methods are marked `payable` to save 24 gas per call
/// @author Michael Elliot <mike@makerdao.com>
/// @author Joshua Levine <joshua@makerdao.com>
/// @author Nick Johnson <arachnid@notdot.net>
/// @author Andreas Bigger <andreas@nascent.xyz>
/// @author Matt Solomon <matt@mattsolomon.dev>
/// Multicall3 contract without the msg.value == valAccumulator check
/// in the aggregate3Value function to enable ETH transfers when called via
/// a delegatecall.
/// Our tasks use delegate calls to call the Multicall3 contract, which then
/// makes calls to targets using the aggregate3Value function. This function
/// includes the check: require(msg.value == valAccumulator, "Multicall3: value mismatch");
/// which will always fail because msg.value is always zero when aggregate3Value is
/// called via a delegate call.
/// Diff with Multicall3: https://www.diffchecker.com/48Cu6U8D/
contract Multicall3NoValueCheck {
    struct Call3Value {
        address target;
        bool allowFailure;
        uint256 value;
        bytes callData;
    }

    struct Result {
        bool success;
        bytes returnData;
    }

    address public immutable thisMulticall3;

    constructor() {
        thisMulticall3 = address(this);
    }

    /// @notice Aggregate calls with a msg value
    /// @notice Reverts if msg.value is less than the sum of the call values
    /// @param calls An array of Call3Value structs
    /// @return returnData An array of Result structs
    function aggregate3Value(Call3Value[] calldata calls) public payable returns (Result[] memory returnData) {
        // only delegate call allowed because for simple
        // calls we don't have the check that makes sure that
        // the msg.value is the sum of the call values
        require(address(this) != thisMulticall3, "Multicall3: only delegate call allowed");
        uint256 length = calls.length;
        returnData = new Result[](length);
        Call3Value calldata calli;
        for (uint256 i = 0; i < length;) {
            Result memory result = returnData[i];
            calli = calls[i];
            (result.success, result.returnData) = calli.target.call{value: calli.value}(calli.callData);
            assembly {
                // Revert if the call fails and failure is not allowed
                // `allowFailure := calldataload(add(calli, 0x20))` and `success := mload(result)`
                if iszero(or(calldataload(add(calli, 0x20)), mload(result))) {
                    // set "Error(string)" signature: bytes32(bytes4(keccak256("Error(string)")))
                    mstore(0x00, 0x08c379a000000000000000000000000000000000000000000000000000000000)
                    // set data offset
                    mstore(0x04, 0x0000000000000000000000000000000000000000000000000000000000000020)
                    // set length of revert string
                    mstore(0x24, 0x0000000000000000000000000000000000000000000000000000000000000017)
                    // set revert string: bytes32(abi.encodePacked("Multicall3: call failed"))
                    mstore(0x44, 0x4d756c746963616c6c333a2063616c6c206661696c6564000000000000000000)
                    revert(0x00, 0x84)
                }
            }
            unchecked {
                ++i;
            }
        }
    }
}
