set dotenv-load

export rpcUrl := env_var('ETH_RPC_URL')
export signatures := env_var_or_default('SIGNATURES', '')
export bundle := "inputs/op"
export location := `pwd`
export OWNER_SAFE := env_var('FOUNDATION_SAFE')

simulate hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  echo "getting signer address..."
  if [ t"$SIMULATE_WITHOUT_LEDGER" == t1 ]
  then
    signer="0x42d27eEA1AD6e22Af6284F609847CB3Cd56B9c64"
  else
    signer=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  fi
  echo "Simulating with: ${signer}"
  echo ""
  forge build
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json" \
    --sender ${signer}

sign hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  echo "getting signer address..."
  signer=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  echo "Signing with: ${signer}"
  echo ""
  forge build
  $(git rev-parse --show-toplevel)/bin/eip712sign --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" -- \
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json"

execute hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  sender=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  forge build
  forge script --fork-url ${ETH_RPC_URL} SignFromJson \
    --sig "runJson(string,bytes)" "${location}/${bundle}.json" ${SIGNATURES} \
    --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" --broadcast \
    --sender ${sender}

add-transaction to sig *params:
  #!/usr/bin/env bash
  cd ../..
  newbundle="${bundle}-new"
  backupbundle="${bundle}-$(date -u '+%Y-%m-%d_%H-%M-%S')"
  DATA=$(cast calldata '{{sig}}' {{params}})
  jq --arg to "{{to}}" --arg data "${DATA}" \
    '.transactions += [{"to": $to, "data": $data}]' \
    "${location}/${bundle}.json" \
    > "${location}/${newbundle}.json"
  mv "${location}/${bundle}.json" "${location}/${backupbundle}.json" 
  mv "${location}/${newbundle}.json" "${location}/${bundle}.json"
  echo "Old bundle backed up to ${backupbundle}.json"
