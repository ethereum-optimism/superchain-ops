set dotenv-load

export rpcUrl := env_var_or_default('ETH_RPC_URL', "https://ethereum.publicnode.com")
export signatures := env_var_or_default('SIGNATURES', '')
export bundle := "remove-signer"
export location := `pwd`

simulate:
  #!/usr/bin/env bash
  cd ../..
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json"

sign hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  echo "getting signer address..."
  signer=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  echo "Signing with: ${signer}"
  echo ""
  $(go env GOPATH)/bin/eip712sign --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" -- \
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json"

execute hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  sender=0xa67399981087005085e632a02F49Eb73dD0AC54A # $(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  forge script SignFromJson \
    --fork-url ${rpcUrl} \
    --sender ${sender} \
    --sig "runJson(string,bytes)" ${location}/${bundle}.json ${signatures}
